WHITESPACE = _{ "\t" }
value_char = _{ !("=" | "|" | WHITESPACE | NEWLINE | SOI | EOI ) ~ ANY }
value = @{ value_char+ }
name = @{ value }
no_value = @{ "_" }
conll = { SOI ~ sentence+ }
sentence = { NEWLINE? ~ token+ }
token = { NEWLINE? ~ id ~ form ~ lemma ~ upos ~ xpos ~ features ~ head ~ deprel ~ enhanced_deps ~ misc }
id = @{ ASCII_DIGIT+ }
form = ${ no_value | value }
lemma = ${ no_value | value }
upos = ${ no_value | value }
xpos = ${ no_value | value }
features = ${ no_value | (feature ~ ("|" ~ feature)*) }
feature = ${ name ~ "=" ~ value}
head = ${ no_value | id }
deprel = ${ no_value | value }
enhanced_deps = ${ no_value | (enhanced_dep ~ ("|" ~ enhanced_dep)*)}
enhanced_dep = ${ id ~ ":" ~ value }
misc = ${ no_value | (feature ~ ("|" ~ feature)*) }
