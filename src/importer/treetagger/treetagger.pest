treetagger = {
    SOI ~ ((end_tag | start_tag | token_line) ~ NEWLINE?)+ ~ EOI
}

TAB        = _{ "\t" }
WHITESPACE = _{ " " | TAB | NEWLINE }

column_value = { (!(TAB | NEWLINE) ~ ANY)+ }

token_line = ${
    column_value ~ (TAB ~ column_value)* ~ &(NEWLINE | EOI)
}

tag_name = @{ !(">" | "<") ~ ASCII_ALPHANUMERIC+ }
attr_id  = @{ ASCII_ALPHANUMERIC+ }
string   = @{ "\"" ~ inner ~ "\"" }
inner    = @{ (!("\"" | "\\") ~ ANY)* ~ (escape ~ inner)? }
escape   = @{ "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | unicode) }
unicode  = @{ "u" ~ ASCII_HEX_DIGIT{4} }

start_tag = {
    "<" ~ tag_name ~ (attr_id ~ "=" ~ string)* ~ ">"
}

end_tag = {
    "</" ~ tag_name ~ ">"
}